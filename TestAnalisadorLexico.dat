/*  Programa para contar as ocorrencias das
    palavras de um texto                    */

program AnaliseDeTexto ;

/*  Variaveis globais  */

var
	char nomes[50][10], palavra[10];
	int i, ntab, posic, nocorr[50];
char c; logic fim;

/*  Funcao para procurar uma palavra na tabela de palavras  */

function int Procura ();
var
	int i, inf, sup, med, posic, compara;
    	logic achou, fimteste;
{
	achou = false; inf = 1; sup = ntab;
	while (!achou && sup >= inf) {
		med = (inf + sup) / 2;
		compara = 0; fimteste = false;
		for (i = 0; !fimteste && compara == 0; i = i+1) {
         		if (palavra[i] < nomes[med][i])
                	compara = ~1;
            	else if (palavra[i] > nomes[med][i])
                	compara = 1;
            	if (palavra[i] == '\0' || nomes[med][i] == '\0')
                	fimteste = true;
		}
		if (compara == 0)
			achou = true;
		else if (compara < 0)
			sup = med - 1;
		else inf = med + 1;
	}
	if (achou) posic = med;
	else posic = ~inf;
	return posic;}

/*  Procedimento para inserir uma palavra na tabela de palavras  */

procedure Inserir (int posic);
var
	int i, j; logic fim;
{
	ntab = ntab + 1;
	for (i = ntab; i >= posic+1; i = i-1) {
        	fim = false;
        	for (j = 0; !fim; j = j+1) {
            	nomes[i][j] = nomes[i-1][j];
            	if (nomes[i][j] == '\0')
                	fim = true;
        	}
		nocorr[i] = nocorr[i-1];
	}
    	fim = false;
    	for (j = 0; !fim; j = j+1) {
        	nomes[posic][j] = palavra[j];
        	if (palavra[j] == '\0')
            	fim = true;
    	}
	nocorr[posic] = 1;
}

/*  Procedimento para escrever a tabela de palavras  */

procedure ExibirTabela ();
var
	int i; int j; logic fim;
{
	write ("          ", "Palavra             ", 
								"   Num. de ocorr.");
	for (i = 1; i <= 50; i = i+1) write ("-");
	for (i = 1; i <= ntab; i = i+1) {
		write ("\n          ");
		fim = false;
		for (j = 0; !fim; j = j+1) {
            if (nomes[i][j] == '\0') fim = true;
            else write (nomes[i][j]);
   		}
		write (" | ", nocorr[i]);	
	}
}

 
/*  Modulo principal  */

{
	ntab = 0;
	write ("Nova palavra? (s/n): ");
	read (c);
	while (c == 's' || c == 'S') {
        	write ("\nDigite a palavra: ");
        	fim = false;
		for (i = 0; !fim; i = i+1) {
            read (palavra[i]);
            if (palavra[i] == '\n') {
                fim = true;
                palavra[i] = '\0';
            }
        	}		
		posic = Procura ();
		if (posic > 0)
			nocorr[posic] = nocorr[posic] + 1;
		else
			call Inserir (~posic);
        	write ("\n\nNova palavra? (s/n): ");
        	read (c);
	}
	call ExibirTabela ();
}
